#!/usr/bin/gawk -f
# USAGE: $0 [PLAYLIST-ADD]... [-- [PLAYLIST-UPDATE]... [-- [PLAYLIST-FILTER]...]]
#
# Create an M3U8 playlist from URLs of PLAYLIST-UPDATE and append new URLs from
# PLAYLIST-ADD.
BEGIN {
	delete playlist[0]
	delete metadata[0]

	inr = 1
	add = ARGC
	filter = ARGC
	for (argi = 1; argi < ARGC; ++argi) {
		if (ARGV[argi] == "--") {
			if (add == ARGC)
				add = inr
			else if (filter == ARGC)
				filter = inr
			delete ARGV[argi]
		} else {
			++inr
		}
	}

	inr = 0
	extname = ""
}

FNR == 1 {
	extinf = ""
	++inr
}

/^#EXTM3U$/ {
	next
}

/^#PLAYLIST:/ {
	if (FNR == 1)
		extname = substr($0, 10)
	next
}

/^#EXTINF:/ {
	extinf = $0
	next
}

/^#/ {
	printf "\033[;1m%s:%d: \033[1;35munknown comment: %s\033[m\n", FILENAME, FNR, $0 > "/dev/tty"
	status = 2
}

{
	if (inr < add || $0 in metadata) {
		if (inr < add ? !playlist[$0] : 0 < playlist[$0])
			playlist[$0] = length(playlist) + (inr < add ? 0 : -99999999)
		metadata[$0] = extinf
	}
	extinf = ""
}

END {
	print "#EXTM3U"
	if (extname)
		print "#PLAYLIST:" extname

	PROCINFO["sorted_in"] = "@val_num_asc"
	for (url in playlist) {
		if (metadata[url])
			print metadata[url]
		print url
	}

	exit status
}
